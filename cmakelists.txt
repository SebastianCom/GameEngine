cmake_minimum_required( VERSION 3.10 )

# Global Settings
set_property( GLOBAL PROPERTY USE_FOLDERS ON )
#set_property( GLOBAL PROPERTY CMAKE_SKIP_INSTALL_ALL_DEPENDENCY TRUE )
#set_property( GLOBAL PROPERTY CMAKE_SKIP_PACKAGE_ALL_DEPENDENCY TRUE )

# Solution
project( GameSolution VERSION 0.0.1 )

# Visual Studio Settings
if( CMAKE_CXX_COMPILER_ID MATCHES "MSVC" )
	# Program Database for Edit and Continue
	set( CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} /ZI" )
#	set( CMAKE_SHARED_LINKER_FLAGS "/SAFESEH:NO" )
#	set( CMAKE_EXE_LINKER_FLAGS "/SAFESEH:NO" )
endif()

###################
# Box2D Library
###################

include( GNUInstallDirs )
add_subdirectory( Framework/Libraries/box2d/src )

###################
# Bullet Libraries
###################

add_compile_options(/wd4244) # 'argument': conversion from 'double' to 'btScalar', possible loss of data
add_compile_options(/wd4267) # '=': conversion from 'size_t' to 'long', possible loss of data
add_compile_options(/wd4305) # 'initializing': truncation from 'double' to 'btScalar'

set( BULLET_VERSION 3.20 )
set( BULLET_PHYSICS_SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/Framework/Libraries/bullet )
add_subdirectory( Framework/Libraries/bullet/src/BulletCollision )
add_subdirectory( Framework/Libraries/bullet/src/BulletDynamics )
add_subdirectory( Framework/Libraries/bullet/src/LinearMath )

add_compile_options(/w44244)
add_compile_options(/w44267)
add_compile_options(/w44305)

###################
# Framework Library
###################

# File Setup
file( GLOB_RECURSE FrameworkSourceFiles
    Framework/Source/*.cpp
    Framework/Source/*.h
    Framework/Libraries/stb/*.h
    Framework/Libraries/rapidjson/*.h
    Framework/Libraries/pcg-cpp/include/*.hpp
)
file( GLOB FrameworkSourceFilesNonRecursive
    Framework/Libraries/imgui/*.cpp
    Framework/Libraries/imgui/*.h
)
list( APPEND FrameworkSourceFiles ${FrameworkSourceFilesNonRecursive} )
source_group( TREE ${CMAKE_CURRENT_SOURCE_DIR}/Framework FILES ${FrameworkSourceFiles} )

# Project Creation
add_library( Framework ${FrameworkSourceFiles} )

# PCH Files
target_precompile_headers( Framework PRIVATE Framework/Source/CoreHeaders.h )
file( GLOB_RECURSE FrameworkPCHFiles ${CMAKE_CURRENT_BINARY_DIR}${CMAKE_FILES_DIRECTORY}/cmake_pch.* )
source_group( "CMake PCH Files" FILES ${FrameworkPCHFiles} )

###################
# Game Project
###################

# File Setup
file( GLOB_RECURSE GameSourceFiles
	Game/Source/*.cpp
	Game/Source/*.h
	Game/Data/Shaders/*.vert
	Game/Data/Shaders/*.frag
	GenerateProjectFiles.bat
)
source_group( TREE ${CMAKE_CURRENT_SOURCE_DIR} FILES ${GameSourceFiles} )

include_directories(
	Game/Source
	Framework/Source
	Framework/Libraries/bullet/src
)

# Project Creation
add_executable( GameProject WIN32 ${GameSourceFiles} )

# PCH Files
target_precompile_headers( GameProject PRIVATE Framework/Source/Framework.h )
file( GLOB_RECURSE GamePCHFiles ${CMAKE_CURRENT_BINARY_DIR}${CMAKE_FILES_DIRECTORY}/cmake_pch.* )
source_group( "CMake PCH Files" FILES ${GamePCHFiles} )

# Libraries to Link
target_link_libraries( GameProject
	Framework
	box2d
	opengl32.lib
	BulletCollision
    	BulletDynamics
    	LinearMath
)

# Visual Studio Settings
if( CMAKE_CXX_COMPILER_ID MATCHES "MSVC" )
	# Debug working directory and Startup Project
	set_property( TARGET GameProject PROPERTY VS_DEBUGGER_WORKING_DIRECTORY "${CMAKE_SOURCE_DIR}/Game" )
	set_property( DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR} PROPERTY VS_STARTUP_PROJECT GameProject )
endif()
